buildscript {
    repositories {
        maven { url "file://${projectDir}/libs" }

        if( project.hasProperty('internalArtifactRepository') ){
            maven { url project.internalArtifactRepository }
        } else {
            maven { url 'https://plugins.gradle.org/m2/' }
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } //spock
            mavenCentral()
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.lv.plugins:gradle-weblogic-plugin:0.4'
    }
}

apply plugin: 'war'
apply plugin: 'com.lv.weblogic'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'


repositories {
    if( project.hasProperty('internalArtifactRepository') ){
        maven { url project.internalArtifactRepository }
    } else {
        mavenCentral()
    }
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '1.4'
    compile group: 'log4j', name: 'log4j', version: '1.2.15', ext: 'jar'

    weblogic files("${projectDir}/libs/wlfullclient-1036.jar")

    // Spock
    compile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

}

// Weblogic configuration

ext.archiveFile = war.archivePath

weblogic {
    adminurl = 't3://localhost:7001'
    user = 'weblogic'
    password = 'welcome1'
    source = archiveFile
    deploymentName = project.name
    targets = 'AdminServer'
    debug = true
    verbose = true
    remote = true
    upload = true
}

// Integration test configuration

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testInt/groovy')
        }
        resources.srcDir file('src/testInt/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task intTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    reports {
        html {
            enabled true
        }
        reports.html.destination = "$buildDir/reports/intTests"
    }
}

// Jacoco configuration

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            'wlfullclient-1036.jar**'
                    ])
        })
    }
}

// Checkstyle configuration

checkstyle {
    //configFile file('downloaded_google_checks.xml') // https://github.com/checkstyle/checkstyle/blob/checkstyle-6.9/src/main/resources/google_checks.xml
    toolVersion '6.9'    // Be sure checkstyle.xml corresponds to this version
    showViolations=false
}

task checkstyleHtml << {
    ant.xslt(in: checkstyleMain.reports.xml.destination,
            style: file('config/checkstyle/checkstyle-noframes-sorted.xsl'),
            out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
}

checkstyleMain.finalizedBy checkstyleHtml
